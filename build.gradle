/*
 * This file is part of Genesis Mod, licensed under the MIT License (MIT).
 *
 * Copyright (c) 2017 Boethie
 * Copyright (c) 2017 contributors
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in
 * all copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN
 * THE SOFTWARE.
 */

buildscript {
    ext.kotlin_version = '1.2.21'
    repositories {
        jcenter()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '2.3-SNAPSHOT'
        classpath group: 'org.jetbrains.kotlin', name: 'kotlin-gradle-plugin', version: kotlin_version
    }
}

plugins {
    id 'com.github.hierynomus.license' version '0.14.0'
    id 'com.github.johnrengelman.shadow' version '1.2.4'
}

apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'kotlin'

version = "0.0.1-SNAPSHOT"
group = "genesis"
archivesBaseName = "genesis"

sourceCompatibility = 1.8
targetCompatibility = 1.8

minecraft {
    version = "1.12.2-14.23.2.2611"
    runDir = "run"
    mappings = "snapshot_20171003"
    replaceIn "GenesisMod.java"
    replace "@VERSION@", project.version
}

license {
    header rootProject.file('HEADER.txt')
    includes(["**/*.java", "**/*.kt"])
    mapping {
        // Use SLASHSTAR_STYLE because Javadoc style makes IntelliJ IDEA
        // think the license headers are dangling Javadocs
        java = 'SLASHSTAR_STYLE'
        kt = 'SLASHSTAR_STYLE'
    }
}

repositories {
    jcenter()
    maven { url "http://dvs1.progwml6.com/files/maven" }
    maven { url "modmaven.k-4u.nl" }
    maven { url "http://tehnut.info/maven" }
    maven { url "http://maven.tterrag.com/" }
    maven { url "http://maven.covers1624.net" }
}

dependencies {
    compile group: 'org.jetbrains.kotlin', name: 'kotlin-stdlib-jre8', version: kotlin_version
    compile group: 'org.jetbrains.kotlin', name: 'kotlin-reflect', version: kotlin_version

    deobfCompile "mezz.jei:jei_1.12.2:4.8.5.138"
    deobfProvided "mcjty.theoneprobe:TheOneProbe-1.12:1.12-1.4.19-11:api" //It uses incompatible mappings ¯\_(ツ)_/¯
    deobfCompile "mcp.mobius.waila:Hwyla:1.8.23-B38_1.12"
}

shadowJar {
    classifier ''
    relocate 'kotlin', 'genesis.repackage.kotlin'
    relocate 'org.intellij', 'genesis.repackage.org.intellij'
    relocate 'org.jetbrains', 'genesis.repackage.org.jetbrains'
}

reobf {
    shadowJar {}
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'

        expand 'version': project.version, 'mcversion': project.minecraft.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

jar {
    manifest {
        attributes 'FMLAT': 'genesis_at.cfg'
    }
}
